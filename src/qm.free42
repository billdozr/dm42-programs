00 { 32-Byte Prgm }
01▸LBL "CONJ"
02 FUNC 11
03 CPX?
04 GTO E
05 CPXMAT?
06 GTO E
07 RTNERR 4
08▸LBL E
09 COMPLEX
10 +/-
11 COMPLEX
12 RTN
13 END

00 { 165-Byte Prgm }
01▸LBL "DIAG"
02 STO "MTMP"
03 CPXMAT?
04 SF 10
05 RCL "MTMP"
06 DIM?
07 STO 00
08 X<>Y
09 X=Y?
10 GTO 01
11 "NOT SQUARE"
12 AVIEW
13 RTN
14▸LBL 01
15 1
16 STO "COUNT"
17 1
18 RCL 00
19 NEWMAT
20 ENTER
21 FS?C 10
22 COMPLEX
23 STO "VTMP"
24▸LBL 02
25 INDEX "MTMP"
26 RCL "COUNT"
27 ENTER
28 STOIJ
29 RCLEL
30 STO "EL"
31 INDEX "VTMP"
32 1
33 RCL "COUNT"
34 STOIJ
35 RCL "EL"
36 STOEL
37 1
38 STO+ "COUNT"
39 RCL 00
40 RCL "COUNT"
41 X≤Y?
42 GTO 02
43 RCL "VTMP"
44 CLV "EL"
45 CLV "COUNT"
46 CLV "VTMP"
47 CLV "MTMP"
48 END

00 { 67-Byte Prgm }
01▸LBL "PRBDE"
02 FUNC 11
03 CPXMAT?
04 GTO E
05 RTNERR 4
06▸LBL E
07 STO "PSI"
08 XEQ "CONJ"
09 STO "PSI*"
10 RCL "PSI"
11 RCL "PSI*"
12 TRANS
13 X<>Y
14 DOT
15 ABS
16 CLV "PSI"
17 CLV "PSI*"
18 END

00 { 113-Byte Prgm }
01▸LBL "PRBDI"
02 FUNC 11
03 CPXMAT?
04 GTO E
05 RTNERR 4
06▸LBL E
07 STO "PSI"
08 XEQ "CONJ"
09 STO "PSI*"
10 RCL "PSI"
11 RCL "PSI*"
12 TRANS
13 ×
14 XEQ "DIAG"
15 STO "PRBDE"
16 RSUM
17 STO "NORMF"
18 RCL "PRBDE"
19 X<>Y
20 ÷
21 COMPLEX
22 CLX
23 R↓
24 CLV "PSI"
25 CLV "PSI*"
26 CLV "PRBDE"
27 CLV "NORMF"
28 END

00 { 83-Byte Prgm }
01▸LBL "EXPV"
02 STO "SV"
03 X<>Y
04 STO "OP"
05 X<>Y
06 ×
07 STO "KET"
08 RCL "SV"
09 CPXMAT?
10 XEQ "CONJ"
11 TRANS
12 STO "BRA"
13 X<>Y
14 DOT
15 CPX?
16 GTO 01
17 GTO 02
18▸LBL 01
19 COMPLEX
20 X≠0?
21 COMPLEX
22 CPX?
23 GTO 02
24 R↓
25▸LBL 02
26 CLV "SV"
27 CLV "OP"
28 CLV "BRA"
29 CLV "KET"
30 RTN
31 END
